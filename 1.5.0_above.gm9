set PREVIEW_MODE "Checking GodMode9 version"

set ERR_MSG_VER "Unsupported version"

# Red Unlocker for 1.5.0 and above
# author : windows-server-2003(QCFium)
# permissions : memory(blue)
# This script will unlock the SysNAND level3 writing permission(Red)
# with only memory writing permission(Blue)

# RUN THIS AT YOUR OWN RISK
# I am not responsible for anything

# check version - check the hash of the top 256KB of the firm
set ERRORMSG $[ERR_MSG_VER] # instead of "'if' command not found" for lower versions
if sha -o -s M:/arm9.mem@6000:40000 AF01396C62963613DFE5F5E36B06C9863ED733F05F14ED73B037C259D3BA2E6C
    ######## 1.5.0
    set GM9_VERSION "1.5.0"
    set ADDRESS M:/arm9.mem@51FB0
elif sha -o -s M:/arm9.mem@6000:40000 31DF2A11BF4514F1B8C8D3B12EA738733CFFA301364499CF33DF4B8B48111C86
    ######## 1.5.1
    set GM9_VERSION "1.5.1"
    set ADDRESS M:/arm9.mem@52638
elif sha -o -s M:/arm9.mem@6000:40000 422FA3B196F8850C4CBB42F886AD1158452BBDE00EA390FD87A952319C711BB1
    ######## 1.6.0
    set GM9_VERSION "1.6.0"
    set ADDRESS M:/arm9.mem@56188
elif sha -o -s M:/arm9.mem@6000:40000 D33C7C683648D1BAD659307F7C93A19006FCFF67317181A537CFE997E5CD2B57
	######## 1.6.1
	set GM9_VERSION "1.6.1\n2nd Anniversary Edition"
	set ADDRESS M:/arm9.mem@56938
else
    # version not supported
    set ERRORMSG $[ERR_MSG_VER]
    cp 0 0 # force fail
end

## actual operations
# ask user
    set PREVIEW_MODE "Version : $[GM9_VERSION]\n \nAsking user"
    set SUCCESSMSG "The red permission unlocked\n \nTo say :\nBe careful when you unlock the blue permission"
    ask -s "RedUnlocker $[GM9_VERSION]\n \nDo you know what you are doing?\nIf not, press B button.\nCONTINUE AT YOUR OWN RISK"

# get the blue permission
    set PREVIEW_MODE "Version : $[GM9_VERSION]\n \nGetting the blue permission"
    set ERRORMSG "This script needs the blue permission."
    allow M:/arm9.mem

# actual unlocking
    set PREVIEW_MODE "Version : $[GM9_VERSION]\n \nUnlocking the red permission"
    set ERRORMSG "Unknown error"
    if chk -o -s $[GM9_VERSION] 1.5.0 # 1.5.0 does not support the 'fill' command
        inject M:/mcu_3ds_regs.mem@80:4 $[ADDRESS]
    else
        fill $[ADDRESS]:4 FF
    end
